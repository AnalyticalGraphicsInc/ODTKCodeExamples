<?xml version="1.0"?>

<!-- ===================================================== -->
<!--  This file sets up the ODTK reader for Yahsat         -->
<!--  derived from very similar Paradigm reader            -->
<!--                                                       -->
<!--  author: jens ramrath, 23 jan 2012                    -->
<!-- ===================================================== -->


<component>

<?component error="true" debug="true"?>

<registration
    description="Yahsat Format"
    progid="TrkDataReader.Yahsat.ODTK6.wsc"
    version="1.00"
    classid="{9E8765D3-5DE0-4cf6-8E0F-8E70141D337F}"
>
</registration>

<public>

   <!-- Properties -->

   <property name="FileName">
      <get internalName="GetFileName"/>
   </property>

   <property name="SupportsMultipleTrackers">
      <get internalName="GetSupportsMultipleTrackers"/>
   </property>

   <property name="SupportsSave">
      <get internalName="GetSupportsSave"/>
   </property>

   <!-- Methods -->

   <method name="Reset"/>

   <method name="OpenFile">
      <parameter name="newVal"/>
      <parameter name="newFile"/>
   </method>

  <method name="CloseFile"/>

  <method name="GetObsSet">
      <parameter name="setCollection"/>
   </method>

   <method name="GetSupportedObsSet">
      <parameter name="setColl"/>
   </method>

   <method name="SaveObs">
      <parameter name="pEnum"/>
      <parameter name="bAppend"/>
   </method>

   <method name="GetRegKey"/>

</public>

<script language="VBScript">
<![CDATA[

    '-------------------------------------------------------------------------
    ' Member variables
    '-------------------------------------------------------------------------

    Option explicit

    Const ForReading   = 1
    Const ForWriting   = 2
    Const ForAppending = 8

    Const eMTRange            = 1
    Const eMTDoppler          = 2
    Const eMTAzimuth          = 11
    Const eMTElevation        = 12

    Const eMTRightAscension   = 18
    Const eMTDeclination      = 19
    Const eMTSBRightAscension = 31
    Const eMTSBDeclination    = 32
    Const eMTSBRange          = 33
    Const eMTSBAzimuth        = 34
    Const eMTSBElevation      = 35

    Const eGroundReceiveTime     = 0
    Const eSatelliteTransmitTime = 1
    Const eGroundTransmitTime    = 2
    Const eSatelliteReceiveTime  = 3

    '-------------------------------------------------------------------------
	' define some global variables we'll be using
    '-------------------------------------------------------------------------

    Dim m_sfileSysObj, m_Stream, m_sFileName, m_Satellite

    Set m_sfileSysObj = CreateObject("Scripting.FileSystemObject")

    m_sFileName = ""

    '-------------------------------------------------------------------------
	' define some regular expression objects that we'll be using
    '-------------------------------------------------------------------------

    dim oRegExp, oMatches, oMatch

	Set oRegExp = new RegExp

    '-------------------------------------------------------------------------
	' Create obsSet and obs objects for use later on
    '-------------------------------------------------------------------------

    Dim version
    version = "6.0"

    Dim textFile, ObjectStr, versionStr, regValue, ODDBObjectFile
    versionStr = "HKEY_LOCAL_MACHINE\SOFTWARE\AGI\ODTK\" & version & "\InstallHome"
    GetRegKey(versionStr)

    ODDBObjectFile = m_sfileSysObj.Buildpath(regValue, "\ODTK\AppData\Scripts\MeasProviders\WindowsScriptComponents\" & "CreatODDBObjects.txt")
    Set textFile = m_sfileSysObj.OpenTextFile(ODDBObjectFile, ForReading)

    Dim pObsSet
    ObjectStr = textFile.ReadLine               ' ObjectStr = "AgMach10_5.AgODObsSet"  or similar

    Set pObsSet = CreateObject(ObjectStr)


    Dim pObs
    ObjectStr = textFile.ReadLine               ' ObjectStr = "IAgODProvideTrackingData5.AgODGenericObs"  or similar
    Set pObs = CreateObject(ObjectStr)

	'-------------------------------------------------------------------------
	' Define look up tables to translate names to ID numbers
    '-------------------------------------------------------------------------

	Dim satIDs
	Set satIDs = CreateObject("Scripting.Dictionary")
	satIDs.Add "Y1A", "1001" 

      		

	Dim facIDs
	Set facIDs = CreateObject("Scripting.Dictionary")
	facIDs.Add "A1", "501"
	facIDs.Add "B2", "502"	
	facIDs.Add "C1", "503"
	

   '-------------------------------------------------------------------------
   ' Function GetFileName()
   '-------------------------------------------------------------------------

    Function GetFileName()

       GetFileName = m_sFileName

    End Function

    '-------------------------------------------------------------------------
    ' SupportsSave property [read-only]
    '-------------------------------------------------------------------------

    Function GetSupportsSave()

       GetSupportsSave = False

    End Function

    '-------------------------------------------------------------------------
    ' SupportsMultipleTrackers property [read-only]
    '-------------------------------------------------------------------------

    Function GetSupportsMultipleTrackers()

       GetSupportsMultipleTrackers = True

    End Function

    '-------------------------------------------------------------------------
    ' Reset method
    '-------------------------------------------------------------------------

    Function Reset()

        if m_sFileName <> "" Then

            Dim newVal
            newVal = m_sFileName
            OpenFile newVal, False

        End if

    End Function

    '-------------------------------------------------------------------------
    ' OpenFile method
    '-------------------------------------------------------------------------

    Function OpenFile(newVal, newFile)

		dim line

        If m_sFileName <> "" Then
            m_Stream.Close
            m_sFileName = ""
        End If

        m_sFileName = newVal

        If newFile Then
            Set m_Stream = m_sfileSysObj.OpenTextFile(m_sFileName, ForWriting, True)
        Else
            Set m_Stream = m_sfileSysObj.OpenTextFile(m_sFileName, ForReading)

			'-------------------------------------------------------------------------
			' Read header information to find the satellite ID
			'-------------------------------------------------------------------------

			m_Satellite     = ""
			oRegExp.pattern = "SAT_ID:=\s+(\S+)"

			Do Until m_Stream.AtEndOfStream

				line = m_Stream.ReadLine
				set oMatches = oRegExp.Execute(line)

				' If we found the satellite ID then we are done.

				if oMatches.Count = 1 then
					m_Satellite = satIDs.Item(oMatches(0).submatches(0))
					exit do
				end if

			loop

		End If

    End Function

	'-------------------------------------------------------------------------
    ' CloseFile method
    '-------------------------------------------------------------------------

    Function CloseFile()

        If m_sFileName <> "" Then
            m_Stream.Close
            m_sFileName = ""
        End If

    End Function

    '-------------------------------------------------------------------------
    ' GetObsSet method
    '-------------------------------------------------------------------------

    Function GetObsSet(setColl)

        dim line, datetime, prevdatetime, obsval, station
        dim meas_type, groundequip, antenna, calstatus, numObsSets

		prevdatetime = ""

		pObsSet.Clear()
		numObsSets = 0

		' Note that we are going to process the entire file of data and return ALL
		' measurements.  We sort of have to because we need to be checking all the
		' time tags to identify obs sets.

		'-------------------------------------------------------------------------
		' parse the line (see sample below)
		'
		' MEAS:= 2012/09/13 17:00:14.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:15.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:16.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:17.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:18.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:19.000 RG SiteA KBM_A12 A1   36453.24609286 GX
		' MEAS:= 2012/09/13 17:00:19.000 AZ SiteA ACU-KA1 A1     185.67340000 ZZ
		' MEAS:= 2012/09/13 17:00:19.000 EL SiteA ACU-KA1 A1      61.34350000 ZZ
		'
		'-------------------------------------------------------------------------
		
		oRegExp.pattern  = "^MEAS:= (\S+\s+\S+)\s+(\S{2})\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)\s+(\S+)"

        Do Until m_Stream.AtEndOfStream or numObsSets > 0

            line = m_Stream.ReadLine
            set oMatches = oRegExp.Execute(line)

            if oMatches.Count = 1 then

                datetime    = oMatches(0).submatches(0)
                meas_type   = oMatches(0).submatches(1)
                station     = oMatches(0).submatches(2)
				groundequip = oMatches(0).submatches(3)
				antenna     = oMatches(0).submatches(4)
                obsval      = oMatches(0).submatches(5)
                calstatus   = oMatches(0).submatches(6)


				pObsSet.Clear()
				pObsSet.Date.unit = "YYYY/MM/DD"
				pObsSet.Date      = datetime

				'-------------------------------------------------------------------------
				' Initialize observation
				'-------------------------------------------------------------------------

				pObs.Date.unit  = "YYYY/MM/DD"
				pObs.Date       = datetime
				pObs.TimeOrigin = eGroundReceiveTime

				'-------------------------------------------------------------------------
				' Set up facility and satellite IDs. Facility must come first for 2-way ranging.
				'-------------------------------------------------------------------------
				
				pObs.TrackerIDs.Clear()
				pObs.TrackerIDs.Insert 0, facIDs(antenna)	' Facility must be first item
				pObs.TrackerIDs.Insert 1, m_Satellite 		' Satellite follows Facility

				Select Case meas_type
					case "RG"
						pObs.MeasureType  = eMTRange
						pObs.value.Unit   = "km"
						pObs.value        = obsval
						pObsSet.Add pObs
					case "AZ"
						pObs.MeasureType  = eMTAzimuth
						pObs.value.Unit   = "deg"
						pObs.value        = obsval
						pObsSet.Add pObs
					case "EL"
						pObs.MeasureType  = eMTElevation
						pObs.value.Unit   = "deg"
						pObs.value        = obsval
						pObsSet.Add pObs
				End Select


				If pObsSet.Count > 0 Then
					setColl.Add pObsSet
					numObsSets = numObsSets + 1
				End If

        	End If

		Loop

        GetObsSet = numObsSets

    End Function  ' GetObsSet

    '-------------------------------------------------------------------------
    ' GetSupportedObsSet method
    '-------------------------------------------------------------------------

    Function GetSupportedObsSet(setColl)
	    Dim numSets
        numSets = 0
        GetSupportedObsSet = numSets
    End Function

    '-------------------------------------------------------------------------
    ' SaveObs method
    '-------------------------------------------------------------------------

    Function SaveObs(pEnum, bAppend)
	    Dim nObsSaved
        nObsSaved = 0
        SaveObs = nObsSaved
    End Function


    Function GetRegKey (regKey)
        Dim objShell
        Set objShell = CreateObject("WScript.Shell")
        regValue = objShell.RegRead(regKey)
        Set objShell = nothing
    End Function

]]>
</script>

</component>

